# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

group("tests") {
  testonly = true
  deps = [
    ":driver_tools_tests",
    "tests",
  ]
}

rustc_library("lib") {
  edition = "2021"
  name = "driver_tools"
  with_unit_tests = true
  deps = [
    "common:lib",
    "connector:lib",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_rust",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_rust",
    "//sdk/fidl/fuchsia.driver.registrar:fuchsia.driver.registrar_rust",
    "//sdk/rust/zx-status",
    "//src/devices/lib/bind",
    "//src/lib/fidl/rust/fidl",
    "//third_party/rust_crates:ansi_term",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:prettytable-rs",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:termion",
  ]
  if (is_host) {
    deps += [ "//src/devices/lib/driver-static-checks:lib" ]
  }
  test_deps = [
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
  ]
  sources = [
    "src/args.rs",
    "src/common.rs",
    "src/lib.rs",
    "src/subcommands/disable/args.rs",
    "src/subcommands/disable/mod.rs",
    "src/subcommands/dump/args.rs",
    "src/subcommands/dump/mod.rs",
    "src/subcommands/list/args.rs",
    "src/subcommands/list/mod.rs",
    "src/subcommands/list_composite_node_specs/args.rs",
    "src/subcommands/list_composite_node_specs/mod.rs",
    "src/subcommands/list_composites/args.rs",
    "src/subcommands/list_composites/mod.rs",
    "src/subcommands/list_devices/args.rs",
    "src/subcommands/list_devices/mod.rs",
    "src/subcommands/list_hosts/args.rs",
    "src/subcommands/list_hosts/mod.rs",
    "src/subcommands/mod.rs",
    "src/subcommands/node/args.rs",
    "src/subcommands/node/common.rs",
    "src/subcommands/node/mod.rs",
    "src/subcommands/node/subcommands/add/args.rs",
    "src/subcommands/node/subcommands/add/mod.rs",
    "src/subcommands/node/subcommands/graph/args.rs",
    "src/subcommands/node/subcommands/graph/mod.rs",
    "src/subcommands/node/subcommands/list/args.rs",
    "src/subcommands/node/subcommands/list/mod.rs",
    "src/subcommands/node/subcommands/mod.rs",
    "src/subcommands/node/subcommands/remove/args.rs",
    "src/subcommands/node/subcommands/remove/mod.rs",
    "src/subcommands/node/subcommands/show/args.rs",
    "src/subcommands/node/subcommands/show/mod.rs",
    "src/subcommands/register/args.rs",
    "src/subcommands/register/mod.rs",
    "src/subcommands/restart/args.rs",
    "src/subcommands/restart/mod.rs",
    "src/subcommands/test_node/args.rs",
    "src/subcommands/test_node/mod.rs",
    "src/subcommands/test_node/subcommands/add/args.rs",
    "src/subcommands/test_node/subcommands/add/mod.rs",
    "src/subcommands/test_node/subcommands/mod.rs",
    "src/subcommands/test_node/subcommands/remove/args.rs",
    "src/subcommands/test_node/subcommands/remove/mod.rs",
  ]
  inputs = [
    "tests/golden/list_composites_verbose",
    "tests/golden/list_composites_verbose_empty_fields",
  ]
}

fuchsia_unittest_package("driver_tools_tests") {
  deps = [ ":lib_test" ]
}

rustc_binary("driver_tools") {
  visibility = [ ":*" ]
  edition = "2021"
  name = "driver"
  with_unit_tests = false
  deps = [
    ":lib",
    "connector:lib",
    "//sdk/fidl/fuchsia.driver.development:fuchsia.driver.development_rust",
    "//sdk/fidl/fuchsia.driver.registrar:fuchsia.driver.registrar_rust",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:async-trait",
  ]
  sources = [ "src/main.rs" ]
  configs += [ "//build/config/rust:bootfs" ]
}

fuchsia_package("package") {
  package_name = "driver"
  deps = [ ":driver_tools" ]
}
